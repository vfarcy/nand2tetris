/j This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=prev, out=incout);
    // use few simple gates to avoid using Mux8Way16
    Or(a=load, b=inc, out=loadorinc);
    Or(a=loadorinc, b=reset, out=change);
    Not(in=reset, out=nreset);
    And(a=load, b=nreset, out=xload);
    And(a=inc,  b=nreset, out=xinc);
    Mux4Way16(a=false, b=in, c=incout, d=in,
              sel[0]=xload, sel[1]=xinc, out=muxout);
    Register(in=muxout, load=change, out=prev, out=out);
}

